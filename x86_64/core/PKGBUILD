# Maintainer: artoo <artoo@artixlinux.org>
# Maintainer: Chris Cromer <cromer@artixlinux.org>
# Contributor: williamh <williamh@gentoo.org>

_ever=1.1
_eurl="https://gitea.artixlinux.org/artix/openrc-extra/archive"
_aurl="https://gitea.artixlinux.org/artix/alpm-hooks"
_commit=3dc7ecf3fb340314a2ada79fe2f1dde738e6ede7

pkgbase=openrc
pkgname=('openrc' 'openrc-bash-completions' 'openrc-zsh-completions' 'openrc-deptree2dot')
pkgver=0.43.4
pkgrel=1
pkgdesc="Gentoo's universal init system"
arch=('x86_64')
url="https://github.com/OpenRC/openrc"
license=('BSD2')
makedepends=('psmisc' 'netifrc' 'eudev' 'zsh' 'perl' 'git')
source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "${pkgbase}-extra-${_ever}.tar.gz::${_eurl}/${_ever}.tar.gz"
        "git+${_aurl}.git#commit=${_commit}"
        'openrc.logrotate'
        'sysctl.conf'
        'artix-rc-conf.patch')
sha256sums=('2df59da9b0af5aebb7e72adc7bab6e372c875e1920ed94156824ebca716e1141'
            '36d5aaaab23f4ce9cdd8a0b558396b0a7f7469bc68987ffdaf9aad10ead85cba'
            'SKIP'
            '0b44210db9770588bd491cd6c0ac9412d99124c6be4c9d3f7d31ec8746072f5c'
            '874e50bd217fef3a2e3d0a18eb316b9b3ddb109b93f3cbf45407170c5bec1d6d'
            '8a256f18c25e67452e91b97f11b5eb2bb513f0014be069a6e8ba50e369f4d9b5')

_args=(
    BRANDING="Artix Linux"
    SYSCONFDIR=/etc
    PREFIX=/usr
    SBINDIR=/usr/bin
    LIBEXECDIR=/usr/lib/openrc
    MKSELINUX=no
    MKPAM=pam
    MKTERMCAP=ncurses
    MKNET=no
    MKSYSVINIT=yes
    MKBASHCOMP=yes
    MKZSHCOMP=yes
    MKPKGCONFIG=yes
)

prepare(){
    cd "${srcdir}/${pkgbase}-${pkgver}"
    sed -i 's:0444:0644:' mk/sys.mk
    patch -Np 1 -i ../artix-rc-conf.patch
}

check(){
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make -k check
}

build(){
    cd "${pkgbase}-${pkgver}"
    make "${_args[@]}"
}

package_openrc() {
    depends=('psmisc' 'netifrc' 'inetutils')
    optdepends=('networkmanager-openrc: networkmanager init script'
                'elogind-openrc: elogind init script'
                'openrc-deptree2dot: Graph the Dependency Tree')
    backup=('etc/rc.conf'
            'etc/conf.d/consolefont'
            'etc/conf.d/keymaps'
            'etc/conf.d/hostname'
            'etc/conf.d/modules'
            'etc/conf.d/hwclock'
            'etc/conf.d/opentmpfiles-dev'
            'etc/conf.d/opentmpfiles-setup'
            'etc/conf.d/udev'
            'etc/conf.d/udev-trigger'
            'etc/conf.d/udev-settle'
            etc/conf.d/agetty.tty{1,2,3,4,5,6})
    provides=('svc-manager' 'init-rc' 'librc.so' 'libeinfo.so')
    conflicts=('svc-manager' 'init-rc')

    cd "${pkgbase}-${pkgver}"

    make DESTDIR="${pkgdir}" "${_args[@]}" install

    install -Dm644 "${srcdir}/${pkgbase}".logrotate "${pkgdir}"/etc/logrotate.d/"${pkgbase}"

    install -d "${pkgdir}"/usr/lib/{openrc/cache,binfmt.d,sysctl.d}

    # sysctl defaults
    install -m755 "${srcdir}"/sysctl.conf "${pkgdir}"/usr/lib/sysctl.d/50-default.conf

    # license
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgbase}"/LICENSE

    # pacman hooks
    cd ${srcdir}/alpm-hooks
    make DESTDIR="${pkgdir}" install_openrc

    # openrc extra; agetty,kmod,udev,tmpfiles,sysusers
    cd "${srcdir}/${pkgbase}-extra"
    make DESTDIR="${pkgdir}" install

    # split bash & zsh completions
    install -d "${srcdir}"/{_bash,_zsh}
    mv -v "${pkgdir}"/usr/share/bash-completion "${srcdir}"/_bash
    mv -v "${pkgdir}"/usr/share/zsh "${srcdir}"/_zsh

    # remove suport dir
    rm -r "${pkgdir}"/usr/share/openrc

    # remove init symlink
    rm -v "${pkgdir}"/usr/bin/init
}

package_openrc-bash-completions() {
    pkgdesc="OpenRC completions for Bash"

    install -d "${pkgdir}"/usr/share
    mv -v "${srcdir}"/_bash/* "${pkgdir}"/usr/share
}

package_openrc-zsh-completions() {
    pkgdesc="OpenRC completions for ZSH"

    install -d "${pkgdir}"/usr/share
    mv -v "${srcdir}"/_zsh/* "${pkgdir}"/usr/share
}

package_openrc-deptree2dot() {
    pkgdesc="Graph the OpenRC Dependency Tree"
    depends=('perl')

    cd "${pkgbase}-${pkgver}"
    install -Dm755 support/deptree2dot/deptree2dot "${pkgdir}"/usr/bin/deptree2dot
}
