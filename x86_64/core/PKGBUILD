# Maintainer: artoo <artoo@artixlinux.org>
# Maintainer: Chris Cromer <cromer@artixlinux.org>
# Contributor: williamh <williamh@gentoo.org>

_url="https://github.com/OpenRC/openrc/archive"

pkgbase=openrc
pkgname=('openrc' 'openrc-bash-completions' 'openrc-zsh-completions')
pkgver=0.42.1
pkgrel=18
pkgdesc="Gentoo's universal init system"
arch=('x86_64')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('BSD2')
makedepends=('psmisc' 'pam' 'netifrc' 'zsh')
source=("${pkgbase}-${pkgver}.tar.gz::${_url}/${pkgver}.tar.gz"
        "${pkgbase}.logrotate"
        'openrc-hook'
        'openrc-install.hook'
        '30-openrc-upgrade.hook'
        'openrc-remove.hook'
        '30-sysctl.hook'
        'sysctl.conf'
        '30-binfmt.hook'
        opentmpfiles-dev.{confd,initd}
        opentmpfiles-setup.{confd,initd}
        'opensysusers.initd'
        'kmod-static-nodes.initd'
        udev.{confd,initd}
        udev-settle.{confd,initd}
        udev-trigger.{confd,initd}
        gcc-10-fix.patch::https://github.com/OpenRC/openrc/commit/375ef42393f3dc6edbaa2cb70c79b2366072db38.patch
        supervise-daemon-fix.patch::https://github.com/OpenRC/openrc/commit/6deda13754f1b60245945e953cce8d97e40e86fc.patch
        openrc-shutdown-fix.patch::https://github.com/OpenRC/openrc/commit/fd852865e06a74ecf8b77ff534fa8053e020160f.patch
        posix-compliant-grep.patch::https://github.com/OpenRC/openrc/commit/a7e7fd2b37a7666f26c2d4de9386b2d04f583b41.patch
        binfmt-fix.patch::https://github.com/OpenRC/openrc/commit/eb610859519292c6164c4ba601d22e642c306beb.patch
        cgroup2-verify.patch::https://github.com/OpenRC/openrc/commit/87cfad3d6cd619c6090e8209092448ae68279b0c.patch
        supervise-log.patch::https://github.com/OpenRC/openrc/commit/2355f1a3f2a4fd62cac6d9af0e94c8731acd4c0f.patch)
sha256sums=('91a01ca6d930a0688fb91338209985de57cac7aa2d37feddacb78fd3d95308e2'
            '0b44210db9770588bd491cd6c0ac9412d99124c6be4c9d3f7d31ec8746072f5c'
            'e405597cced825154afde5c5521403e509ae36c3f8ca7bab6e79d63734b13919'
            'd95237e2018f4a647770ef8a2e2f04d13edbdeccf117493d2d1b2424ede80eeb'
            '3ef8682540a038c239c4e07881cb084501d7996df2e0395b70a0a3953c7d00b2'
            '6c699cab557f689c7b9e333efa9e5029fe8e7b11d001ccd5c63bca2519d6d9e5'
            '28ec075d30096bd54499e994f76f8526460474f1e4c33607045c1a71b0684263'
            '874e50bd217fef3a2e3d0a18eb316b9b3ddb109b93f3cbf45407170c5bec1d6d'
            '28aa413fd78bca98bb23294eeb3f49149825a2c2f97a7ee10039385f5e8e50e9'
            '505b7fa013d90b1775439c06b6b0d420f877768cc59eaff5ffb1493a9e28b4ba'
            'c357e37ef59845954add9ccf89eff07beae0bd5274fdaa3ebf32da61c9d38436'
            '505b7fa013d90b1775439c06b6b0d420f877768cc59eaff5ffb1493a9e28b4ba'
            '68779b6f4d8f4bcc206ff0e7ddab2c4cbff029727404cffa94079c1bf06094c8'
            '6476fd66ead2ad43129e3135656f4fde64addddabb6b7405665b3cf8db4aa6ad'
            '3ecbe7761b92a123a5a3d395ec3d9e090b0dfba7910a5987b4a2298c20a7e571'
            '40b0c151000fd670ad64b07c19b315bb15322dd610a1105404bc2aae4a2b59e4'
            'f5dda3cf258e34708c20121d4ace5c79559acd00d392bc010566b4c4ea94dd37'
            '2b79cbe451d0132dd8c381b9a1701162bd1eecfab6d1b3f729d7528f9b11327f'
            'e89315857de1ba62455b7ab93a9e20120ff20981aa3b9479a32dbf9be15926dd'
            '24579fea33a859ce031630a518c738a45535ba1a4699f0c89ef41ecaca8178fc'
            '1642c64f2ad2929a602ab3c63e832c3ad3fab43ea636a6316c6de8308794b652'
            'addb1bf115b811fba3d120f5c68535ec07f32a55e570b661f1e5575f74327bac'
            '654dada5ffe56d6ae10d0ea4f9cb62c8c6c39a5398cd02561419a8bbf3f686b7'
            '5576a8b6c9fbe79ede027ed053f922da60d1cbe818803677f5aa5532f98ffac4'
            '46f6e809aabb629212a739af7f5177930e9c4a7f2adfdfc59911d778d3716ec9'
            '7e5d7dc888327ef99637fa7207ac9470d72cc37198ddd7ba2cdf72d10a1c7031'
            '41c6598da76bcdcdc8d9349f559bc6b587a6f6dec55bf7c79669af2b28af43e1'
            '4adbf07385bed6e24975cb44673551ecc393f1765206406f3a1ed51ffa367046')

_args=(
    SYSCONFDIR=/etc
    PREFIX=/usr
    SBINDIR=/usr/bin
    LIBEXECDIR=/usr/lib/openrc
    MKSELINUX=no
    MKPAM=pam
    MKTERMCAP=ncurses
    MKNET=no
    MKSYSVINIT=yes
    MKBASHCOMP=yes
    MKZSHCOMP=yes
    MKPKGCONFIG=yes
)

if [ -f /usr/lib/os-release ]; then
    . /usr/lib/os-release
    _args+=(BRANDING="${NAME}")
else
    _args+=(BRANDING='Unknown Linux')
fi

prepare(){
    cd "${srcdir}/${pkgbase}-${pkgver}"
    sed -i 's:0444:0644:' mk/sys.mk
    patch -Np 1 -i ../gcc-10-fix.patch
    patch -Np 1 -i ../supervise-daemon-fix.patch
    patch -Np 1 -i ../openrc-shutdown-fix.patch
    patch -Np 1 -i ../posix-compliant-grep.patch
    patch -Np 1 -i ../binfmt-fix.patch
    patch -Np 1 -i ../cgroup2-verify.patch
    patch -Np 1 -i ../supervise-log.patch
}

check(){
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make -k check
}

build(){
    cd "${pkgbase}-${pkgver}"
    make "${_args[@]}"
}

package_openrc() {
    depends=('psmisc' 'pam' 'netifrc' 'inetutils')
    optdepends=('networkmanager-openrc: networkmanager init script'
                'elogind-openrc: elogind init script')
    backup=('etc/rc.conf'
            'etc/conf.d/consolefont'
            'etc/conf.d/keymaps'
            'etc/conf.d/hostname'
            'etc/conf.d/modules'
            'etc/conf.d/hwclock'
            'etc/conf.d/opentmpfiles-dev'
            'etc/conf.d/opentmpfiles-setup'
            'etc/conf.d/udev'
            'etc/conf.d/udev-trigger'
            'etc/conf.d/udev-settle'
            etc/conf.d/agetty.tty{1,2,3,4,5,6})
    provides=('librc.so' 'libeinfo.so'
            'svc-manager' 'init-rc')
    conflicts=('svc-manager' 'init-rc'
                'opentmpfiles-openrc' 'opensysusers-openrc' 'kmod-openrc' 'eudev-openrc')
    replaces=('opentmpfiles-openrc' 'opensysusers-openrc' 'kmod-openrc' 'eudev-openrc')

    cd "${pkgbase}-${pkgver}"

    make DESTDIR="${pkgdir}" "${_args[@]}" install

    install -Dm644 "${srcdir}/${pkgbase}".logrotate "${pkgdir}"/etc/logrotate.d/"${pkgbase}"

    sed -e 's/#unicode="NO"/unicode="YES"/' \
        -e 's/#rc_logger="NO"/rc_logger="YES"/' \
        -e 's/#rc_controller_cgroups="YES"/rc_controller_cgroups="YES"/' \
        -e 's|#rc_shell=/bin/sh|rc_shell=/usr/bin/sulogin|' \
        -i "${pkgdir}/etc/rc.conf"

    install -d "${pkgdir}"/usr/lib/{openrc/cache,binfmt.d,sysctl.d}

    # getty
    sed -e 's|#baud=""|baud="38400"|' \
        -e 's|#term_type="linux"|term_type="linux"|' \
        -e 's|#agetty_options=""|agetty_options=""|' \
        -i ${pkgdir}/etc/conf.d/agetty

    for num in 1 2 3 4 5 6;do
        cp -v ${pkgdir}/etc/conf.d/agetty ${pkgdir}/etc/conf.d/agetty.tty$num
        ln -sf /etc/init.d/agetty ${pkgdir}/etc/init.d/agetty.tty$num
        ln -sf /etc/init.d/agetty.tty$num ${pkgdir}/etc/runlevels/default/agetty.tty$num
    done

    # sysctl defaults
    install -m755 "${srcdir}"/sysctl.conf "${pkgdir}"/usr/lib/sysctl.d/50-default.conf

    # pacman hooks
    install -Dm755 "${srcdir}"/openrc-hook "${pkgdir}"/usr/share/libalpm/scripts/openrc-hook
    install -Dm644 -t "${pkgdir}"/usr/share/libalpm/hooks "${srcdir}"/*.hook

    # license
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgbase}"/LICENSE

    # split bash completions
    install -d "${srcdir}"/_bash-completion/usr/share/bash-completion/completions
    mv -v "${pkgdir}"/usr/share/bash-completion/completions/* "${srcdir}"/_bash-completion/usr/share/bash-completion/completions

    # split zsh completions
    install -d "${srcdir}"/_zsh/usr/share/zsh/site-functions
    mv -v "${pkgdir}"/usr/share/zsh/site-functions/* "${srcdir}"/_zsh/usr/share/zsh/site-functions

    # remove init symlink
    rm -v "${pkgdir}"/usr/bin/init

    # sysusers & tmpfiles
    install -m644 "${srcdir}"/opentmpfiles-dev.confd "${pkgdir}"/etc/conf.d/opentmpfiles-dev
    install -m755 "${srcdir}"/opentmpfiles-dev.initd "${pkgdir}"/etc/init.d/opentmpfiles-dev

    install -m644 "${srcdir}"/opentmpfiles-setup.confd "${pkgdir}"/etc/conf.d/opentmpfiles-setup
    install -m755 "${srcdir}"/opentmpfiles-setup.initd "${pkgdir}"/etc/init.d/opentmpfiles-setup

    install -m755 "${srcdir}"/opensysusers.initd "${pkgdir}"/etc/init.d/opensysusers

    ln -snf /etc/init.d/opensysusers "${pkgdir}"/etc/runlevels/boot/opensysusers
    ln -snf /etc/init.d/opentmpfiles-dev "${pkgdir}"/etc/runlevels/sysinit/opentmpfiles-dev
    ln -snf /etc/init.d/opentmpfiles-setup "${pkgdir}"/etc/runlevels/boot/opentmpfiles-setup

    # kmod staticc nodes
    install -m755 "${srcdir}"/kmod-static-nodes.initd "${pkgdir}"/etc/init.d/kmod-static-nodes
    ln -snf /etc/init.d/kmod-static-nodes "${pkgdir}"/etc/runlevels/sysinit/kmod-static-nodes

    # udev
    install -m644 "${srcdir}"/udev.confd "${pkgdir}"/etc/conf.d/udev
    install -m755 "${srcdir}"/udev.initd "${pkgdir}"/etc/init.d/udev

    install -m644 "${srcdir}"/udev-trigger.confd "${pkgdir}"/etc/conf.d/udev-trigger
    install -m755 "${srcdir}"/udev-trigger.initd "${pkgdir}"/etc/init.d/udev-trigger

    install -m644 "${srcdir}"/udev-settle.confd "${pkgdir}"/etc/conf.d/udev-settle
    install -m755 "${srcdir}"/udev-settle.initd "${pkgdir}"/etc/init.d/udev-settle

    ln -snf /etc/init.d/udev "${pkgdir}"/etc/runlevels/sysinit/udev
    ln -snf /etc/init.d/udev-trigger "${pkgdir}"/etc/runlevels/sysinit/udev-trigger
}

package_openrc-bash-completions() {
    depends=('openrc' 'bash')
    pkgdesc="OpenRC completions for Bash"

    mv -v "${srcdir}"/_bash-completion/* "${pkgdir}"
}

package_openrc-zsh-completions() {
    depends=('openrc' 'zsh')
    pkgdesc="OpenRC completions for ZSH"

    mv -v "${srcdir}"/_zsh/* "${pkgdir}"
}
